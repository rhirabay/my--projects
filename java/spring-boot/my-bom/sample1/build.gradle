plugins {
    // bom生成に必要なプラグイン
    id 'java-platform'
    id 'maven-publish'
}

group = 'hirabay.bom'
version = '0.0.1'

javaPlatform {
    allowDependencies()
}

repositories {
    mavenCentral()
}

dependencies {
    // constraintsでくくるとbom利用者側でversion指定がいらなくなる
    // ライブラリを使用していない場合は何も起きない（勝手にライブラリが追加されたりしない）
    constraints {
        api 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.2'
        api 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.2'
    }

    // bomの中にbomを含めることも可能
    api platform('org.springframework.boot:spring-boot-dependencies:3.0.8')
    api platform('org.springframework.cloud:spring-cloud-dependencies:2022.0.3')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            group = 'hirabay.bom' // 未指定の場合、プロジェクトのgroupを使用
            artifactId = 'sample-bom' // 未指定の場合、プロジェクト名を使用
            version = '0.0.1'  // 未指定の場合、プロジェクトのversionを使用
            from components.javaPlatform
        }
    }
}
