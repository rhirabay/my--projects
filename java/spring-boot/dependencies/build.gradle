plugins {
    id 'java-platform'
    id 'maven-publish'
}

group = 'rhirabay.bom'
version = '0.0.1'

javaPlatform {
    allowDependencies()
}

repositories {
    mavenCentral()
}

dependencies {
    // ライブライのバージョンの固定
    constraints {
        // cf. https://mockk.io/
        api 'io.mockk:mockk:1.12.3'

        // cf. https://github.com/Ninja-Squad/springmockk
        // https://mvnrepository.com/artifact/com.ninja-squad/springmockk
        // https://github.com/Ninja-Squad/springmockk/releases
        api 'com.ninja-squad:springmockk:4.0.2'

        // cf. https://mybatis.org/spring-boot-starter/mybatis-spring-boot-autoconfigure/#requirements
        def mybatisVersion = '3.0.3'
        api "org.mybatis.spring.boot:mybatis-spring-boot-starter:${mybatisVersion}"
        api "org.mybatis.spring.boot:mybatis-spring-boot-starter-test:${mybatisVersion}"

        // UserAgent parser
        // https://mvnrepository.com/artifact/com.github.ua-parser/uap-java
        api 'com.github.ua-parser:uap-java:1.6.1'

        def springdocVersion = '2.6.0'
        api "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"
        api "org.springdoc:springdoc-openapi-starter-webflux-ui:${springdocVersion}"
    }

    // BOM
    api platform('org.springframework.boot:spring-boot-dependencies:3.4.1')
    api platform('org.springframework.cloud:spring-cloud-dependencies:2024.0.0')
    api platform('org.springframework.ai:spring-ai-bom:1.0.0-M4')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.javaPlatform
        }
    }
}
